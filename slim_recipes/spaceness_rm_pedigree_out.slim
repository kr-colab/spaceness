initialize() {

    initializeSLiMModelType("nonWF");
    initializeSLiMOptions(keepPedigrees=T,dimensionality="xy");
    initializeTreeSeq(checkCoalescence=T);

	 sigma=0.2;

    defineConstant("K", 5);  // carrying-capacity per unit square (roughly)
    defineConstant("SD", sigma);  // sigma_D, the dispersal distance
    defineConstant("SI", sigma);  // sigma_I, the spatial interaction distance
    defineConstant("SM", sigma);  // sigma_M, the mate choice distance
    defineConstant("L", 4);    // mean lifetime at stationarity
    defineConstant("W", 50.0);  // width and height of the simulated area
    defineConstant("G", 1e8);  // genome length
    defineConstant("FECUN", 1/L); // mean fecundity
    defineConstant("RHO", FECUN/((1+FECUN) * K)); // constant in spatial competition function

    initializeMutationType("m1", 0.5, "g", 0.0, 2);
    initializeGenomicElementType("g1", m1, 1.0);
    initializeGenomicElement(g1, 0, G-1);
    initializeMutationRate(0.0);
    initializeRecombinationRate(1e-9);  
    
    // spatial competition
    initializeInteractionType(1, "xy", reciprocal=T, maxDistance=SI * 3);
    i1.setInteractionFunction("n", 1.0/(2*PI*SI^2), SI);
    
    // mate choice
    initializeInteractionType(2, "xy", reciprocal=T, maxDistance=SM * 3);
    i2.setInteractionFunction("n", 1.0/(2*PI*SM^2), SM);

}

reproduction() {
    mate=p1.sampleIndividuals(1);
    if (mate.size()) {
        nOff = rpois(1, FECUN);
        individual.tag = individual.tag + nOff;
        for (i in seqLen(nOff)) {
            pos = c(runif(1,0,W),runif(1,0,W));
            if (p1.pointInBounds(pos)) {
            offspring = subpop.addCrossed(individual, mate);
          	offspring.setSpatialPosition(pos);
          	offspring.tag = 0;
          	}
        }
    }
    return;
}

1 early() {
    sim.addSubpop("p1", asInteger(K * W * W));
    p1.setSpatialBounds(c(0, 0, W, W));
    // random initial positions
    for (ind in p1.individuals) {
        ind.setSpatialPosition(p1.pointUniform());
        ind.tag=0;
    }
    i1.evaluate();
    
    writeFile("/Users/cj/Desktop/spaceviz/10kpeds/rm_simout_"+SM+"_.txt","timestep"+" "+"ind_ID"+" "+"parent_1"+" "+"parent_2"+" "+"n_offspring"+" "+"x"+" "+"y",F);
}

early() {
    i1.evaluate(p1);
    inds = p1.individuals;
    competition = i1.totalOfNeighborStrengths(inds);
    inds.fitnessScaling = pmin(0.95, 1/(1 + RHO * competition));    
    // decrease fitness proportional to distance from an edge
    inds.fitnessScaling = inds.fitnessScaling * pmin(sqrt(inds.x/SI), 1.0) * pmin(sqrt(inds.y/SI), 1.0) * pmin(sqrt((W - inds.x)/SI), 1.0) * pmin(sqrt((W - inds.y)/SI), 1.0);
}

1: late() {
	// to be ready for mate choice
   i2.evaluate(p1);
}

2: late() {
    if(sim.generation >100){
    	  //sim.treeSeqRememberIndividuals(p1.individuals);
    	  for(x in p1.individuals){
    	  		writeFile("/Users/cj/Desktop/spaceviz/10kpeds/rm_simout_"+SM+"_.txt",sim.generation+" "+x.pedigreeID+" "+x.pedigreeParentIDs[0]+" "+x.pedigreeParentIDs[1]+" "+x.tag+" "+x.x+" "+x.y,T);
    	  }
    	  //sim.treeSeqOutput("/Users/cj/Desktop/spaceviz/gen_"+sim.generation+".trees");
    	}
}

200 late() {
	//outname=outpath+"_"+format("%.0f",runif(1,1e6,1e7-1));
   //sim.treeSeqOutput(outname);
   sim.simulationFinished();
}
